# Compute the Ackermann function ack(a, b)
# The Ackermann function grows faster than any any primitive recursive function. It also takes a _very_ long time to run unless the inputs are tiny.
.CONST a 3
.CONST b 2

..ROM 0
CALL start
RETURN

..ROM 1
# setup
.LABEL start
PUSH a
PUSH b
CALL ack # ack(a, b)
OUTPUT 1.0
RETURN

# s = ack(a, b)
# PUSH a PUSH b CALL ack POP s
.LABEL ack
POP r0 # b
DUP POPNOOP # a
.USEFLAGS
POP r1 # a
BRANCH !Z case2
# a == 0
PUSH r0 # b
INC # b+1
RETURN

# a != 0
.LABEL case2
PUSH r0 POPNOOP # b
.USEFLAGS
BRANCH !Z case3 # b != 0
PUSH r1 # a
DEC # a-1
PUSH 1
CALL ack # ack(a-1, 1)
RETURN

.LABEL case3
PUSH r1 #a
DEC #a-1
PUSH r1 #a
PUSH r0 #b
DEC # b-1
CALL ack # ack(a, b-1)
CALL ack # ack(a-1, ack(a, b-1))
RETURN